%TALLER VER 0.0.1    ,MX--,
;%Taller ver 0.0.1,MX--,
;
; *******************  DEFINICION DE NEMONICOS  ******************
;
;   ENTRADAS
;   ========
DEF IEMER    I01
; Entrada del boton de emergencia
DEF IREF0X   I02
; Origen de maquina eje X
DEF IREF0Z   I04
; Origen de maquina eje Z
DEF IREF0T   I06
; Origen de maquina torreta
DEF ICLAMP   I08
; Torreta campleada
DEF IUNCLAMP I10
; Torreta descampleada
DEF IALAX    I03
; Entrada Alarma eje X
DEF IALAZ    I05
; Entrada Alarma eje Z
DEF IALAT    I07
; Entrada Alarma torreta
DEF MACHON   I16
; Entrada Machine ON
DEF REGETEMP I14
; Alarma de temperatura de la resistencia de regeneracion
DEF SENAIR   I12
; Sensor de presion de aire
DEF LLUB190  I67
; Sensor de bajo lubricante 190
DEF LLUB960  I69
; Sensor de bajo lubricante 960
DEF DOOR     I65
; Sensor de puerta cerrada
DEF SPOVLOA  I71
; SPINDLE OVERLOAD
DEF SPPOWFAI I73
; SPINDLE POWER FAILURE
DEF SPSU     I75
;SPINDLE UP TO FREQUENCY
DEF SPALAR   I77
; SPINDLE ALARM
;
;
;   SALIDAS
;   ========
;
DEF OEMER    O01
; PLC emergencia
; DEF OSYSENA  O02
; SYSTEM ENABLE
DEF OSERONX  O03
; Activa driver servo x
DEF OSERONZ  O05
; Activa driver servo z
DEF OSERONT  O07
; Activa driver servo torreta
DEF OALRESX  O02
; Resetea alarma servo x
DEF OALRESZ  O04
; Resetea alarm servo z
DEF OALREST  O06
; Reseta alarma torreta
DEF OUNCLAMP O48
; Cuando activado descamplea torreta
DEF OPARTCAT O46
; Part catcher
DEF OLUBAIR  O44
; Lubricacion con Aire
DEF OSPSTOP  O42
; Detener spindle
DEF OALRESSP O40
; Spindle reset alarma
DEF OSPFW    O38
; Spindle habilitar direccion positiva
DEF OSPRW    O36
; Spindle habilitar direccion reversa
;
;
;   CONSTANTES
;   ============
DEF SPVMIN 500000
; spindle velocidad m√≠nima
;
;
;   MARCAS
;   ============
;
;
;
; ******************** PROGAMA INICIAL **************************
CY1                     ; CICLO INICIAL
()                      ; NIVEL LOGICO ALTO, ejecuta siempre
=     ERA M1 149        ; BORRA LAS MARCAS
=     ERA M152 159      ; BORRA LAS MARCAS
=     ERA M161 2000     ; BORRA LAS MARCAS
=     ERA R1 20         ; BORRA LOS REGISTROS
=     ERA R23 254       ; BORRA LOS REGISTROS
=     ERA T1 255        ; BORRA LOS TEMPORIZADORES
=     ERA O1 255        ; BORRA LAS SALIDAS
=     SET GEAR1         ; SETEA GAMA UNO
=     SET OALRESX       ; reset alarma eje x
=     SET OALRESZ       ; reset alarma eje z
=     SET OALREST       ; reset alarma torreta
=     SET OALRESSP      ; reset alarma spindle
=     SET AUXEND        ; avisar al CNC que puede continuar
END                     ; FIN DEL CICLO INICIAL
;
;
;
; ********************* PROGAMA PRINCIPAL **********************
;
PRG                     ; PROGAMA PRINCIPAL
REA                     ; VALORES REALES
;
;                              FUNCIONES Ms
;
;DFU MSTROBE                         ; SI HAY UNA EME
;                  = TG1 1 40        ; TIEMPO DE EMES
;
;DFU TSTROBE                         ; SI HAY UNA EME
;                  = TG1 1 40        ; TIEMPO DE EMES
;                  = MOV TBCD R5 108
;
DFU MSTROBE OR DFU TSTROBE = TG1 10 100
;
;                                SERVOS
;
;DRSTAF1 = B1R251          ; X status
;DRSTAS1 = B0R251
;
;
( I3 AND I16 ) = SERVOXON = O3
( I5 AND I16 ) = SERVOZON = O5
IEMER = /EMERGEN;
()               = /STOP           ; plc en estop
() = /FEEDHOL;
START = CYSTART;
INCYCLE = TIMERON;
;
I2=DECELX;
I4=DECELZ;
I6=DECELY;
;NOT T10               = AUXEND
;
() = NOT O48 = NOT O46 = NOT O44
;-----------Cabezal---------
;() = O36 = NOT O38 = NOT O40 = NOT O42;
() = GEAR1
() = CNCRD(SREAL,R10,M1000)
DFU B0R563 AND NOT M119 = CNCEX1(M19,M1111)
DFU M19 AND NOT AUXEND = SET M119
M119 AND CPS R10 EQ 0 = SET SERVOSON
DFU M55 OR DFD M3 OR DFD M4 = RES M119
                            = RES SERVOSON
((DFU M3 OR DFU M4 OR DFU M19) OR (DM03 OR DM04 OR DM19)) = SET SPENAS
;                                                          = SET O1
M55 = TG2 12 500
T18 OR T12 = RES M55
           = RES SPENAS
           = RES O1
() = CNCRD(MPS45,R105,M1000)
DFU B5R561 = TG1 18 R105
CPS R10 EQ 0 = TRS 18
NOT O3 = DRENAS
;
DFU MSTROBE AND CPS MBCD* EQ $1 = M1
DFU MSTROBE AND CPS MBCD* EQ $2 = M2
DFU MSTROBE AND CPS MBCD* EQ $3 = M3
DFU MSTROBE AND CPS MBCD* EQ $4 = M4
DFU MSTROBE AND CPS MBCD* EQ $5 = M5
DFU MSTROBE AND CPS MBCD* EQ $8 = M8
DFU MSTROBE AND CPS MBCD* EQ $9 = M9
DFU MSTROBE AND CPS MBCD* EQ $10 = M10
DFU MSTROBE AND CPS MBCD* EQ $11 = M11
DFU MSTROBE AND CPS MBCD* EQ $12 = M12
DFU MSTROBE AND CPS MBCD* EQ $13 = M13
DFU MSTROBE AND CPS MBCD* EQ $19 = M19
DFU MSTROBE AND CPS MBCD* EQ $20 = M20
DFU MSTROBE AND CPS MBCD* EQ $21 = M21
DFU MSTROBE AND CPS MBCD* EQ $24 = M24
DFU MSTROBE AND CPS MBCD* EQ $25 = M25
DFU MSTROBE AND CPS MBCD* EQ $29 = M29
DFU MSTROBE AND CPS MBCD* EQ $30 = M30
DFU MSTROBE AND CPS MBCD* EQ $200 = M200
DFU MSTROBE AND CPS MBCD* EQ $201 = M201
DFU MSTROBE AND CPS MBCD* EQ $202 = M202
DFU MSTROBE AND CPS MBCD* EQ $203 = M203
DFU MSTROBE AND CPS MBCD* EQ $204 = M204
IMA DFD M201 = SET M1020 = SET M1021
IMA DFD M202 = RES M1020 = SET M1021
IMA M1021 AND ( DFD I8 OR DFD I10 ) = SET AUXEND = RES M1021
DFU M1021 = RES AUXEND
IMA DFD M203 = RES AUXEND = SET M1998
= CNCEX(G91 G0 Y.125, M2000)
M1998 AND IMA DFU INPOSY = SET  AUXEND = RES M1998
IMA DFD M204 = RES AUXEND = SET M1998
= CNCEX(G91 G0 Y-.125, M2000)
IMA DFD M205 = RES AUXEND = SET M1998
= CNCEX(G91 G0 Y-36, M2000)
IMA DFD M206 = RES AUXEND = SET M1998
= CNCEX(G91 G0 Y36, M2000)
CPS R563 NE R30 = CPL M1900
() = MOV R563 R30
DFU M1900 AND CPS R563 EQ $40 = M201
DFU M1900 AND CPS R563 EQ $20 = M202
DFU M1900 AND CPS R563 EQ $10 = M203
DFU M1900 AND CPS R563 EQ $8 = M204
DFU M1900 AND CPS R563 EQ $4 = M205
DFU M1900 AND CPS R563 EQ $2 = M206
;
; CONTROL DIRECCION SPINDLE
( ) = CNCRD(SPEED, R11, M1001 )
( DM03 OR DM19 ) AND ( CPS R11 GE 500000 )  = O38
;( DFU M05 OR ( ( DFU DM03 OR DFU DM04 ) AND ( CPS R10 LT 50 ) ) )  = O36 = O38
( DM05 ) OR ( CPS R11 LT 500000 ) = O40
( DM04 ) AND ( CPS R11 GE 500000 ) = O36
;
;
; CONTROL TORRETA
( I7 AND I16 ) = SERVOYON; CONTROL SERVO
DFU TSTROBE OR DFU M1100 = CNCRD(TOOL, R20, M2000) = SET M1999
IMA DFU M1999 AND CPS R20 LE $10 = JMP L1
IMA DFU M1999 = SBS R20 6 R20
L1
IMA DFU M1999 = SBS R20 1 R20
= MLS R20 -36 R12
= RES M1999
( NOT I10 ) = O7
;( NOT I10 AND NOT M1020 ) = O7
M1002 OR M1100 OR M1020 = O48; CONTROL ACTUADOR
( ) = CNCRD(POSY, R200, M2000)
= CNCRD(FLWEY, R201, M2000)
= CNCRD(DISTY, R202, M2000)
= CNCRD(TPOSY, R203, M2000)
DFU M202 = RES M201
;
; CAMBIO T
DFU TSTROBE = RES AUXEND = SET M1002
= MOV TBCD R13
DFU TSTROBE AND CPS R13 GE $10 = SBS R13 6 R13
IMA DFD I10 AND M1002 = SET M1004
= SBS R13 1 R13
= RES M1007
= MLS -36 R13 R23
= CNCWR(R23 ,GUP200,M1006)
= CNCEX(G90 G0 Y P200, M1006)
= CNCEX((PLCM 1007=1), M1006)
IMA DFU M1007 AND M1004 = RES M1002 = RES M1004
DFD I8 = SET AUXEND
;
; RESET TOOL CHANGER
DFU M200 = RES AUXEND = SET M1100
DFD I10 AND M1100 = RES M1117
= CNCEX(G74 Y, M1107)
;= CNCEX(G4, M1107)
;= CNCEX(G90 G0 YP2000, M1107)
;= CNCEX(G92Y0, M1107)
;= CNCEX(G4, M1107)
= CNCEX((PLCM 1117=1), M1107)
DFU M1117 = RES M1118
= CNCWR(R12,GUP200,M2000)
;= CNCEX(G90 G0 Y P200, M1107)
;= CNCEX(G4, M1107)
= CNCEX((PLCM 1118=1),M1107)
DFU M1118 = RES M1100
DFD  I8 = SET AUXEND
END
