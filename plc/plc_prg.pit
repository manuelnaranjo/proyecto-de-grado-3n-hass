%TALLER VER 0.1.0    ,MX--,
;%Taller ver 0.1.0,MX--,
;
; *******************  DEFINICION DE NEMONICOS  ******************
;
;   ENTRADAS
;   ========
DEF IEMER    I01
; Entrada del boton de emergencia
DEF IREF0X   I02
; Origen de maquina eje X
DEF IREF0Z   I04
; Origen de maquina eje Z
DEF IREF0T   I06
; Origen de maquina torreta
DEF ICLAMP   I08
; Torreta campleada
DEF IUNCLAMP I10
; Torreta descampleada
DEF IEJEXOK  I03
; Entrada Alarma eje X
DEF IEJEZOK  I05
; Entrada Alarma eje Z
DEF IEJETOK  I07
; Entrada Alarma torreta
DEF IMACHON  I16
; Entrada Machine ON
DEF IREGTEMP I09
; Alarma de temperatura de la resistencia de regeneracion
DEF ISENAIR  I12
; Sensor de presion de aire
DEF ILLUB190 I67
; Sensor de bajo lubricante 190
DEF ILLUB960 I69
; Sensor de bajo lubricante 960
;DEF IDOOR    I65
; Sensor de puerta cerrada
DEF ISPOVLOA I71
; Spindle Sobrecarga
DEF ISPPOWAL I73
; Spindle Falla Energia
DEF ISPSU    I75
; Spindle Frequencia Alcanzada
DEF ISPALAR  I70
; Spindle Alarma
DEF ILOWHIDR I14
; Baja presión en sistema hidraulico NA
DEF ITAILFIN1 I11
; Contra punta cerca pieza
DEF ITAILFIN2 I13
; Contra punta punto medio
DEF ITAILFIN3 I15
; Contra punta alejada pieza
DEF IPEDALCLA I77
; Pedal de clampeo/desclampeo de pieza
DEF IPEDALTAI I84
;
;
;
;   SALIDAS
;   ========
;
DEF OEMER    O01
; PLC emergencia
; DEF OSYSENA  O02
; SYSTEM ENABLE
DEF OSERONX  O03
; Activa driver servo x
DEF OSERONZ  O05
; Activa driver servo z
DEF OSERONT  O07
; Activa driver servo torreta
DEF OALRESX  O02
; Resetea alarma servo x
DEF OALRESZ  O04
; Resetea alarm servo z
DEF OALREST  O06
; Reseta alarma torreta
DEF OUNCLAMP O48
; Cuando activado descamplea torreta
DEF OPARTCAT O46
; Part catcher
DEF OLUBAIR  O44
; Lubricacion con Aire
DEF OSPSTOP  O42
; Detener spindle
DEF OALRESSP O40
; Spindle reset alarma
DEF OSPFW    O38
; Spindle habilitar direccion positiva
DEF OSPRW    O36
; Spindle habilitar direccion reversa
;
DEF OTAILR   O33
; Alejar contra punta de la pieza
DEF OTAILF   O35
; Acercar contra punta a la pieza
DEF OTAILHS  O39
; Mover contrapunta a toda velocidad
DEF OSPCLAMP O37
; Liberar pieza
;
DEF OLUZVERD O45
; Prender durante ciclo de trabajo
DEF OLUZROJA O47
; Prender en caso de alarma
;
DEF OCHIPFW O41
; activar recolector de viruta
DEF OCHIPRW O43
; invertir sentido de giro del recolector de virtua
;
DEF OCOOLANT O08;
; activar salida de aceite soluble refrigerante
;
;
;
;   CONSTANTES
;   ============
DEF SPVMIN 500000
; spindle velocidad minima, por abajo de 50rpm no regula el driver
DEF SPVERR   50000
DEF NSPVERR -50000
; spindle error de velocidad tolerable al momento del cambio de velocidad 5rpm
DEF DISTTOOL 36
; una herramienta cada 36 grados, sentido negativo
;
DEF TIMECLA  100
; 75 ms timeout al clampear
DEF TIMEUCLA 50
; 50 ms timeout al desclampear
;
DEF TIMEPACA 1500
; part catcher tarda 1500ms en realizar todo el recorrido
;
DEF TIMETAIL 5500
; tiempo a velocidad lenta entre que pasa fin1 y llega al usillo
DEF TAILZON0 1
; contra punta entre fin 1 y pieza (zona 0)
DEF TAILZON1 2
; contra punta entre fin 1 y fin 2 (zona 1)
DEF TAILZON2 3
; contra punta entre fin 2 y fin 3 (zona 2)
DEF TAILFIN3 4
; contra punta en fin 3
;
DEF TIMELOWA 700
; si se detecta baja presión de aire por 700ms entonces mostramos mensaje
;
DEF TIMEUNMA 1000
; si no se logra descamplear en Xms entonces mostramos mensaje
DEF TIMECLMA 1000
; si no se logra camplear en Xms entonces mostramos mensaje
;
;
DEF LUBSTART 120000
; cuando se enciende la máquina lubricar por 2 minutos
DEF LUBMANUA 300000
; cuando se solicita lubricación manual por teclado hacerlo por 5 minutos
DEF LUB1HOUR 300000
; cuando los ejes se han movido una hora lubricar por 5 minutos
DEF LUBDISTA 240000
; distancia acumulada por los ejes para activar lubricacion
DEF ONEHOUR  3600000
; 1 hora en ms 60m = 360 * 1000ms
;
;   MARCAS
;   ============
DEF MTEMP    M1000
; Marca usada para CNCRD, CNCWR y CNCEX
DEF MANCLAMP M1010
; Marca clampeo torreta manual
DEF MANCLAAU M1011
; Marca auxiliar clampeo torreta manual
DEF MANTAU   M1012
; Marca auxiliar de que se mando avance manual a la torreta
DEF MTHOME   M1020
; Inicializar torreta de herramientas
DEF MTHOMEA1 M1021
; auxiliar inicializar torreta herramientas 1
DEF MTHOMEA2 M1022
; auxiliar inicializar torreta herramientas 2
DEF MTHOMEA3 M1023
; auxiliar inicializar torreta herramientas 3
DEF MTHOMEA0 M1024
; auxiliar inicializar torreta herramientas 0
DEF MTCHANGE M1030
; cambio de herramienta en progreso por comando T
DEF MTCHANA0 M1031
; cambio de herramienta en progreso por comando T auxiliar 0
DEF MTCHANA1 M1032
; cambio de herramienta en progreso por comando T auxiliar 1
DEF MTCHANA2 M1033
; cambio de herramienta en progreso por comando T auxiliar 2
DEF MKCHAN   M1034
; cambio de herramienta pedido por teclado
DEF MKCHAN1  M1035
; marca auxiliar 1 para cambio de herramienta por teclado
DEF MKCHAN2  M1036
; marca auxiliar 2 para cambio de herramienta pedido por teclado
DEF MTOOLF   M1025
; auxiliar de cambio de herramienta a misma herramienta
DEF MDELTAT  M1026
; auxiliar para marcar que la diferencia entre la posición real y la teoríca
; cumple con los requisitos
DEF MTSTROBE M1027
; permite recordar que se encuentra en un ciclo de cambio de herramienta
;
DEF MCLAMPED M1040
; torreta clampeada
DEF MUCLAMPE M1041
; torreta descampleada
;
DEF MACKSPIN M1050
; usado para hacer el ACK de los comandos del spindle M03, M04, M05, M19
DEF MACKM5   M1051
; usado para hacer ACK de M5
DEF MACKMSTR M1052
; usado para hacer ACK a comandos M3, M4, o cambio de velocidad
DEF MREVOK   M1053
; usado para marcar que velocidad de spindle llego a valor aceptable
;
;
DEF MTIMTAIL M1060
; usado para controlar timer para control de la contra punta
DEF MTAILFI3 M1061
; usado para marcar al llegar contra punta a fin3
DEF MTAILZO2 M1062
; usado para marcar al llegar contra punta a zona2
DEF MTAILZO1 M1063
; usado para marcar al llegar contra punta a zona1
DEF MTAILZO0 M1064
; usado para marcar al llegar contra punta a zona0
DEF MTAILRDO M1065
; usado para marcar fin de proceso de retroceso
DEF MTAILFDO M1066
; usado para detectar flancos en T4
DEF MT4      M1067
; usado para marcar contra punta programada
DEF MTAILPRO M1068
; usado para marcar direccion contra punta por pedal
DEF MTAILPED M1069
;
; control de clampeo de spindle
; modo fijación interior / exterior 1 en modo fijación interior
; esta opcion se guarda en una marca que no se borra al reiniciar
DEF MDIAMINT M1070
; pieza atrapada
DEF MPARTCLA M1081
; spindle quieto
DEF MSPINDEA M1082
;
; usado para el control temporazidor del part-catcher
DEF MPARTCAT M1090
;
; usado para operaciones que retienen auxend
DEF MAUXEND  M1091
;
; usados para marcar estados de alarma
DEF MALARMLP M1094
DEF MALARMHP M1095
DEF MLOWAIRP M1096
;
; marcas temporizadores
DEF MT1  M1101
DEF MT2  M1102
DEF MT3  M1103
DEF MT4  M1104
DEF MT5  M1105
DEF MT6  M1106
DEF MT7  M1107
DEF MT8  M1108
DEF MT9  M1109
DEF MT10 M1110
;
;
DEF MT31 M1111
DEF MT32 M1112
DEF MT33 M1113
;
;
;
;   REGISTROS
;   ============
DEF RSREAL   R10
; Registro utilizado para guardar SREAL del CNC
DEF RSPEED   R11
; Registro utilizado para guardar velocidad programada del spindle
DEF RSTEMP   R12
; Registro utilizado para almacenar el promedio entre SREAL y SPEED cuando hay
; cambios de velocidad
DEF RTOOL    R13
; Registro utilizado para guardar la herramienta seleccionada -1
DEF RTOOLD   R14
; herramienta seleccionada en grados
DEF RTBCD    R15
; proxima herramienta pasada por comando T
DEF RTBCDD   R16
; proxima herramienta pasada por comando T en gradosx
DEF ROPMODE  R17
; modo de operacion
DEF RHANDWHE R18
; multiplicador volate
DEF RMAXDISX R20
; maxima distancia recorrida en X antes de lubricar
DEF RMAXDISZ R21
; maxima distancia recorrida en Z antes de lubricar
DEF RDISTX   R22
; distancia recorrida en X desde ultimo reincio
DEF RDISTZ   R23
; distancia recorrida en Z desde ultimo reinicio
DEF RDISTTEM R24
; registro temporal usado para borrar contadores
DEF RPOSTAIL R300
; posicion contra punta
;          posibles valores:
;                   0 - No inicializado
;                   1 - Pieza
;                   2 - Fin 1
;                   3 - Fin 2
;                   4 - Fin 3
DEF RPOST    R25
; posicion real de torreta
DEF RDELTAT  R26
; diferencia de posicion de torreta
DEF RPOSTM   R27
; usado para cambio de herramienta manual
DEF RFLWEY   R202
; debug error posicion torreta
DEF RTPOSY   R203
; debug posicion teorica torreta
DEF RAPOSY   R204
;
;
;
;   TIMERS
;   ============
;   T1 usado para esperar previo a desclampeo torreta
;   T2 usado para esperar previo a clampeo torreta
;   T3 usado control dirección de spindle
;   T4 usado para contar el tiempo que transcurre desde que la contra punta
;   entra a zona 0 hasta que la misma haya llegado a la pieza, como no hay
;   sensores disponibles no puedo saber si llego a tocar
;   T5 usado para control de tiempo del part-catcher
;   T6 usado para control de AUXEND en aquellos comandos que sólo cambian el
;   estado interno del plc
;   T7 usado para verificar si la presión de aire es baja de manera sostenida
;   y no sólo durante la operación
;   T8 usado para alertar al usuario si por alguna razón no se logro descamplear
;   la torre de herramientas en el tiempo estipulado
;   T9 usado para alertar al usuario si por alguna razón no se logro camplear en
;   el tiempo estipulado
;   T10 usado para lubricacion al arranque
;   T11 usado para lubricacion manual
;   T12 usado para acumular tiempo de movimiento
;   T13 usado para lubricacion automatica por tiempo
;   T14 usado para lubricacion automatica por distancia
;
;   CONTADORES
;   ============
;
;   MENSAJES
;   ============
DEF MSGLOAIR MSG1;
DEF MSGREGTE ERR2;
DEF MSGSPOVL ERR3;
DEF MSGSPPOW ERR4;
DEF MSGLLUB1 MSG5;
DEF MSGLLUB2 MSG6;
DEF MSGUNCLA MSG7;
DEF MSGCLAM  MSG8;
DEF MSGNOPAR ERR9;
DEF MSGNOMAC ERR10;
DEF MSGEJEX  ERR11;
DEF MSGEJEZ  ERR12;
DEF MSGTORRE ERR13;
DEF MSGSPALA ERR14;
DEF MSGLOHID ERR15;
DEF MSGLUBRI MSG16;
;
; ******************** PROGAMA INICIAL **************************
CY1                     ; CICLO INICIAL
()                      ; NIVEL LOGICO ALTO, ejecuta siempre
=     ERA M1 149        ; BORRA LAS MARCAS
=     ERA M152 159      ; BORRA LAS MARCAS
=     ERA M161 1059     ; BORRA LAS MARCAS
=     ERA M1080 2000    ; BORRA LAS MARCAS
=     ERA R1 20         ; BORRA LOS REGISTROS
=     ERA R23 254       ; BORRA LOS REGISTROS
=     ERA T1 255        ; BORRA LOS TEMPORIZADORES
=     ERA O1 255        ; BORRA LAS SALIDAS
=     SET GEAR1         ; SETEA GAMA UNO
=     SET OALRESX       ; reset alarma eje x
=     SET OALRESZ       ; reset alarma eje z
=     SET OALREST       ; reset alarma torreta
=     RES OALRESSP      ; reset alarma spindle
=     SET AUXEND        ; avisar al CNC que puede continuar
=     RES OUNCLAMP      ; clampear
=     RES OPARTCAT      ; desactivar part catcher
=     RES OLUBAIR       ; desactivar lubricación
=     SET OSPSTOP       ; frenar spindle
=     TG1 10 LUBSTART   ; activar lubricación en inicio
=     TG2 12 ONEHOUR    ; contador de 1h
=     CNCRD(ATPOSY, RPOST, MTEMP); cargar posicion eje torre
=     DVS RPOST 10000 RPOST; pasar a grados
=     MOV RPOST RTBCDD  ; inicializar registro de posicion de torreta
=     SET MREVOK        ; avisar que velocidad esta dentro de rango
END                     ; FIN DEL CICLO INICIAL
;
;
;
; ********************* PROGAMA PRINCIPAL **********************
;
PRG                     ; PROGAMA PRINCIPAL
REA                     ; VALORES REALES
;
; --------- control de alarmas -----------
; alarmas y mensajes generales
NOT MT7 AND NOT ISENAIR = MSGLOAIR; mostrar que tenemos poco aire
NOT IREGTEMP = MSGREGTE; sobre-calentamiento del regenerador del spindle
ISPOVLOA = MSGSPOVL; spindle sobre cargado
ISPPOWAL = MSGSPPOW; spindle con problema de suministro de energía
DFD ISPALAR  = MSGSPALA; spindle alarma general
DFD ILOWHIDR = MSGLOHID; baja presión en el usillo y contra punta
NOT ILLUB190 = MSGLLUB1; bajo nivel de aceite
NOT ILLUB960 AND OLUBAIR = MSGLLUB2; baja presión de aire para lubricar
NOT IEJEXOK = MSGEJEX; problema en eje X
NOT IEJEZOK = MSGEJEZ; problema en eje Z
NOT IEJETOK = MSGTORRE; problema en torreta
OLUBAIR = MSGLUBRI; mostrar al usuario que estamos lubricando
;
; mostrar al usuario que no se pudo iniciar el ciclo
START AND NOT MPARTCLA = MSGNOPAR; no tenemos pieza no podemos arrancar
START AND NOT IMACHON  = MSGNOMAC; el usuario no puede apretar inicio de ciclo
                                 ; sin prender la máquina
;
; alarma de alta prioridad, si se da esta condición el ciclo de mecanizado se
; detiene al instante.
IEMER OR NOT ILOWHIDR OR NOT ISPOVLOA OR NOT ISPALAR = MALARMHP;
MALARMHP = /EMERGEN;
;
; alarmas de baja prioridad, si se da cualquier condición entonces el ciclo de
; mecanizado se deja terminar, pero se alerta al usuario de que algo esta mal
; para que decida cuando terminar el ciclo de trabajo, sin dañar la máquina
IREGTEMP OR ISPPOWAL = MALARMLP;
;
; estados de alarma que requieren temporización
DFD ISENAIR = TG1 7 TIMELOWA;
T7 = MT7;
DFD MT7 = SET MLOWAIRP;
DFU ISENAIR = RES MLOWAIRP;
;
; clampeo y descampleo de herramienta
DFU MTCHANGE OR DFU MTHOME OR DFU MANCLAMP = TG1 8 TIMEUNMA
DFD MTCHANGE OR DFD MTHOME OR DFD MANCLAMP = TG1 9 TIMEUNMA
T8 = MT8;
T9 = MT9;
DFD MT8 AND NOT OSERONT = SET MSGUNCLA
DFU OSERONT = RES MSGUNCLA
DFD MT9 AND ICLAMP = SET MSGCLAM
DFD ICLAMP = RES MSGCLAM
;
;
;
; canal plc en stop
()    = /STOP
; habilitar avance de ejes siempre que velocidad este alcanzada
MREVOK  = /FEEDHOL;
; no permitir iniciar programa a menos que la pieza se encuentra agarrada
START AND MPARTCLA AND SERVOXON AND SERVOZON = CYSTART;
;
; cuando se encuentre en ciclo
INCYCLE = TIMERON ; activar temporizador
= OLUZVERD        ; indicar correctamente al usuario
;
CNCREADY AND /ALARM AND IEMER = OEMER;
;
; habilitar ejes si es posible
( IEJEXOK AND IMACHON AND NOT LOPEN ) = SERVOXON = OSERONX
( IEJEZOK AND IMACHON AND NOT LOPEN ) = SERVOZON = OSERONZ
;
; Conectar sensores de inicio máquina
IREF0X=DECELX;
IREF0Z=DECELZ;
IREF0T=DECELY;
;
;
; -----------Cabezal---------
() = GEAR1                  ; unico cambio
= SPENAS
= DRENAS
= SERVOSON
;
;
; ------- manejo de teclado izq pantalla ------------------------
DFU B6R563 = TG1 11 LUBMANUA ; pedido manual lubricacion
;
; -----------Marcas enviadas por el CNC --------------------
;DFU MSTROBE AND CPS MBCD* EQ $0 =  M0    ; parada programada
DFU MSTROBE AND CPS MBCD* EQ $1 =  M1    ; parada opcional
DFU MSTROBE AND CPS MBCD* EQ $2 =  M2    ; simil m30
DFU MSTROBE AND CPS MBCD* EQ $3 =  M3    ; spindle a favor reloj
DFU MSTROBE AND CPS MBCD* EQ $4 =  M4    ; spindle contra reloj
DFU MSTROBE AND CPS MBCD* EQ $5 =  M5    ; parar spindle
DFU MSTROBE AND CPS MBCD* EQ $8 =  M8    ; activar refrigerante
DFU MSTROBE AND CPS MBCD* EQ $9 =  M9    ; desactivar refrigerante
DFU MSTROBE AND CPS MBCD* EQ $10 = M10
DFU MSTROBE AND CPS MBCD* EQ $11 = M11
DFU MSTROBE AND CPS MBCD* EQ $12 = M12
DFU MSTROBE AND CPS MBCD* EQ $13 = M13
DFU MSTROBE AND CPS MBCD* EQ $19 = M19   ; spindle lazo cerrado
DFU MSTROBE AND CPS MBCD* EQ $20 = M20
DFU MSTROBE AND CPS MBCD* EQ $21 = M21
DFU MSTROBE AND CPS MBCD* EQ $24 = M24
DFU MSTROBE AND CPS MBCD* EQ $25 = M25
DFU MSTROBE AND CPS MBCD* EQ $29 = M29
DFU MSTROBE AND CPS MBCD* EQ $30 = M30   ; fin de programa
;
; ------------ comandos macro personalizados --------------------------
DFU MSTROBE AND CPS MBCD* EQ $31  = SET M31 ; activar saca viruta sentido directo
DFU MSTROBE AND CPS MBCD* EQ $32  = SET M32 ; activar saca viruta sentido reverso
DFU MSTROBE AND CPS MBCD* EQ $33  = SET M33 ; desactivar saca viruta
DFU MSTROBE AND CPS MBCD* EQ $53  = M53  ; activar part catcher
DFU MSTROBE AND CPS MBCD* EQ $63  = M63  ; desactivar part catcher
DFU MSTROBE AND CPS MBCD* EQ $200 = M200 ; inicializar torreta
DFU MSTROBE AND CPS MBCD* EQ $50  = M50  ; fijación interna
DFU MSTROBE AND CPS MBCD* EQ $51  = M51  ; fijación externa
;
;
; ---------------- control contra punta --------------------------
; retroceso rapido
DFU MSTROBE AND CPS MBCD* EQ $300 = SET M300 ; hasta tope 1
DFU MSTROBE AND CPS MBCD* EQ $301 = SET M301 ; hasta tope 2
DFU MSTROBE AND CPS MBCD* EQ $302 = SET M302 ; hasta tope 3
; avance rapudo hasta tope X luego lento por TIMETAIL ms
DFU MSTROBE AND CPS MBCD* EQ $310 = SET M310 ; rapido hasta tope 1
DFU MSTROBE AND CPS MBCD* EQ $311 = SET M311 ; rapido hasta tope 2
DFU MSTROBE AND CPS MBCD* EQ $312 = SET M312 ; lento
;
; ----------------- control de sistema de lubricacion ------------
ENABLEX OR ENABLEZ = TEN 12; solo acumular tiempo cuando se mueven X o Z
T12 = TG1 13 LUB1HOUR ; activar lubricacion una vez transcurrida 1h de trabajo
T13 = TRS 12 = TG2 12 ONEHOUR; reiniciar contador de 1 hora
;
; cargar parametros configurables por el usuario para setear distancias a
; recorrer en X y en Z antes de lubricar
() = CNCRD(GUP 2010, RMAXDISX, MTEMP) = CNCRD(GUP 2011, RMAXDISZ, MTEMP)
() = CNCRD(DISTX, RDISTX, MTEMP) = CNCRD(DISTZ, RDISTZ, MTEMP)
CPS RDISTX GE RMAXDISX OR CPS RDISTZ GE RMAXDISZ
= TG1 14 LUBDISTA ; activar en monostable para lubricar
= MOV 0 RDISTTEM
= CNCWR(RDISTTEM,DISTX,MTEMP) = CNCWR(RDISTTEM,DISTZ,MTEMP);reiniciar distancias
;
;
;
; activación de lubricacion
T10 OR T11 OR T13 OR T14 = OLUBAIR
DFD OLUBAIR = TRS 10 = TRS 11 = TRS 13 = TRS 14; reiniciar timers
;
; -------- Control por teclado de cambio de herramienta --------
; usuario presiono tecla, no hay código en ejecución y torre clampeada
DFU B3R563 AND NOT CYSTART = SET MKCHAN = SET MKCHAN2
;
; una vez desclampeado y mientras el usuario presione el boton realizar avance
; mientras el usuario siga presionando
OSERONT AND MKCHAN AND B3R563 AND NOT MKCHAN1 = SET MKCHAN1
= CNCEX(G91 G0 Y 36, MTEMP) ; avanzar 36 grados
= CNCEX((PLCMM 1035=0), MTEMP) ; bajar marca cuando se produce ciclo
;
DFD MKCHAN1 AND NOT B3R563 AND MKCHAN = RES MKCHAN2; clampear
;
; esperar que clampee
DFD ICLAMP AND MKCHAN = CNCRD(ATPOSY, RPOSTM, MTEMP);cargar posicion de herramienta
= DVS RPOSTM 10000 RPOSTM; pasar a grados
= MDS RPOSTM 360   RPOSTM  ; pasar a rango 0 <= X <= 360
= DVS RPOSTM 36    RPOSTM  ; pasar a numero de herramienta -1
= ADS RPOSTM 1     RPOSTM  ; pasar a numero de herramienta
= CNCWR(RPOSTM,GUP 201, MTEMP);
= CNCEX1(G91 TP201, MTEMP); actualizar número de herramienta en cnc
= RES MKCHAN; finalizar cambio manual
;
; -------- control de parada bajo pedido -----------------------
DFU RESETOUT OR DFU M30 OR DFU M2 = RES OCOOLANT; desactivar soluble
= SET M33      ; desactivar saca viruta
;
; -------- control de parada en caso de alarma -----------------
DFD /ALARM OR DFU LOPEN = RES OUNCLAMP
= RES MTCHANGE = RES MTHOME = RES MUCLAMPE
= RES MCLAMPED = RES MTCHANA2 = RES MTCHANA1
= RES MTCHANA0 = RES MTHOMEA1 = RES MTHOMEA2
= RES MTSTROBE ; liberar cambio manual de herramienta
= SET PLCABORT ; abortar cualquier movimiento pendiente del eje T
= SET OLUZROJA ; mostrar alarma con la luz
= RES OCOOLANT = SET M33 ; desactivar saca viruta y soluble
= RES MKCHAN = RES MKCHAN1 = RES MKCHAN2; cancelar cambio manual
;
DFU /ALARM OR DFD LOPEN = RES OLUZROJA ; desactivar señal de alarma
;
;
; -------- Control de refrigerante -----------------------------
DFU B10R563 = CPL OCOOLANT; control por teclado
DFU M8 = SET OCOOLANT; control por codigo ISO
DFU M9 = RES OCOOLANT;
;
; -------- Control de saca viruta ------------------------------
DFU B2R563 AND M31 = SET M32; cambiar sentido
DFU B2R563 AND M32 = SET M31; cambiar sentido
DFU B1R563 AND IMA NOT OCHIPFW = SET M31 ; activar hacia adelante
DFU B1R563 AND IMA OCHIPFW = SET M33 ; desactivar
;
DFU M31 = SET OCHIPFW = RES OCHIPRW = RES M32 = RES M33; activar hacia adelante
DFU M32 = SET OCHIPRW = RES OCHIPFW = RES M31 = RES M33; activar hacia atras
DFU M33 = RES OCHIPFW = RES OCHIPRW = RES M31 = RES M32; desactivar
;
;
;--------- Control manual de contra punta -----------------------
; si llega a fin3 entonces esta en fin 3
DFU ITAILFIN3 = MOV TAILFIN3 RPOSTAIL
; si llega a fin2 y se aleja de la pieza o si se alejo de fin3
; se encuentra en en zona 2
(DFU ITAILFIN2 AND OTAILR) OR DFD ITAILFIN3 = MOV TAILZON2 RPOSTAIL;
; si llega a fin1 y se aleja de la pieza, o si se acerca a la pieza y toca fin 2
; se encuentra en zona 1
(DFU ITAILFIN1 AND OTAILR) OR (DFD ITAILFIN2 AND OTAILF) = MOV TAILZON1 RPOSTAIL
; si se aleja de fin1 y se acerca a la pieza entonces alcanzo zona 0
DFD ITAILFIN1 AND OTAILF = MOV TAILZON0 RPOSTAIL
; cualquier otro caso no se inicializo y se mueve lentamente hasta entrar
; en estado válido, esto sólo sucede la primera vez que se ejecute esté código
; ya que el registro mantiene el valor luego de perder la energía y no es borrado.
;
; setear marcas para detectar transiciones
CPS RPOSTAIL EQ TAILFIN3 = MTAILFI3
CPS RPOSTAIL EQ TAILZON2 = MTAILZO2
CPS RPOSTAIL EQ TAILZON1 = MTAILZO1
CPS RPOSTAIL EQ TAILZON0 = MTAILZO0
;
; desactivar contra punta cuando llega a fin3
DFU MTAILFI3 AND OTAILR = RES OTAILR = RES OTAILHS
;
;
; marcar inicio de retroceso programado
DFU M300 OR DFU M301 OR DFU M302
= SET MTAILRDO
= RES AUXEND
;
; marcar fin de retroceso programado
(M300 AND DFU ITAILFIN1) OR
(M301 AND DFU ITAILFIN2) OR
(M302 AND DFU ITAILFIN3)
= RES MTAILRDO
= RES M300 = RES M301 = RES M302
= SET AUXEND
;
; marcar inicio avance programado
DFU M310 OR DFU M311 OR DFU M312
= SET MTAILFDO
= RES AUXEND
; marcar fin de avance programado
T4 = MT4
DFD MT4
= RES MTAILFDO
= SET AUXEND
= RES M310 = RES M311 = RES M312
= RES MTIMTAIL
;
; control avance contra punta
DFU B0R563 OR DFU MTAILFDO = SET OTAILF
DFD B0R563 OR DFD MTAILFDO = RES OTAILF
;
; control retroceso contra punta
DFU B7R563 OR DFU MTAILRDO = SET OTAILR
DFD B7R563 OR DFD MTAILRDO = RES OTAILR
;
; control velocidad rapida contra punta
DFU MTAILRDO OR
DFU B15R563 OR
MTAILFDO AND (
         ( NOT MTAILZO0 AND M310 ) OR
         ( NOT MTAILZO0 AND NOT MTAILZO1 AND M311 ) OR
         ( NOT MTAILZO0 AND NOT MTAILZO1 AND NOT MTAILZO2 AND M312)
)
= SET OTAILHS
DFD MTAILRDO OR DFD B15R563 = RES OTAILHS
; desactivar alta velocidad al entrar a zona 0 si M310
; desactivar alta velocidad al entrar a zona 1 si M311
; desactivar alta velocidad al entrar a zona 2 si M312
OTAILF AND (
       (DFU MTAILZO0 AND M310) OR
       (DFU MTAILZO1 AND M311) OR
       (DFU MTAILZO2 AND M312)
) = RES OTAILHS
= SET MTIMTAIL ; inicializar contador
MTIMTAIL = TG4 4 TIMETAIL
;
;
; -------- Control Direccion Spindle ----------------------------
() = CNCRD(SPEED, RSPEED, MTEMP )
( DM03 ) AND ( CPS RSPEED GE SPVMIN ) = OSPFW
( DM05 ) OR ( CPS RSPEED LT SPVMIN ) = OSPSTOP
( DM04 ) AND ( CPS RSPEED GE SPVMIN ) = OSPRW
;
DFU M5 OR ((DFU M3 OR DFU M4) AND OSPSTOP) = SET MACKM5
= RES AUXEND ; si el usuario solicita M5 o M3 o M4 con velocidad menor a la mínima
; esperar hasta que el usillo baje su velocidad debajo del mínimo, y responder
; via auxend
MACKM5 AND CPS RSREAL LT SPVMIN = SET AUXEND = RES MACKM5
;
DFU M3 OR DFU M4 OR DFU M19 AND REVOK = SET MACKSPIN = RES MREVOK
= RES AUXEND
MACKSPIN = TG1 3 20 ;  por 20ms retener auxend
NOT T3 AND MACKSPIN = SET AUXEND
= RES MACKSPIN
DFU REVOK = SET MREVOK ; no reiniciar hasta que se alcance nueva velocidad
;
; si usuario solicita cambio de velocidad esperar hasta que la diferencia entre
; la velocidad solicitada y la real sea menor al 5%
;DFU M3 OR DFU M4 OR DFU M19 = SET MACKMSTR = RES AUXEND
;DFD REVOK = RES AUXEND ; cambio de velocidad, no dejar seguir bloque
;DFU REVOK = SET AUXEND
;
; -------- Control automatico Torreta herramientas --------------
( IEJETOK AND IMACHON AND NOT LOPEN ) = SERVOYON; habilitar lazo eje salvo
                                                ; que existan alarmas
;
; control con retardos para evitar que lazo de control se active durante
; proceso de cambio de estado del actuador
DFU MTCHANGE OR DFU MTHOME OR DFU MANCLAMP OR DFU MKCHAN2=SET OUNCLAMP;descamplear
DFD IUNCLAMP = SET MUCLAMPE       ; marcar cuando se detecta desclampe
MUCLAMPE = TG4 1 TIMEUCLA         ; esperar Xms luego de descamplear
NOT IUNCLAMP AND NOT T1 AND MUCLAMPE = RES MUCLAMPE ; limpiar marca
DFD MUCLAMPE AND NOT LOPEN = SET OSERONT ; cuando la marca se limpia, energizar
                                         ; servo
;
DFU LOPEN = RES OSERONT; si se muestra algun error entonces
DFD LOPEN AND OUNCLAMP = SET OSERONT; LOPEN cambia de estado y debemos
; desenergizar los ejes, para que los mismos no se mueven
;
;
; desenergizar servo
DFD MTCHANGE OR DFD MTHOME OR DFD MANCLAMP OR DFD MKCHAN2 = RES OSERONT
IMA DFD OSERONT AND IMA NOT LOPEN = SET MCLAMPED ; servo se desenergiza
MCLAMPED = TG4 2 TIMECLA      ; esperar Xms previo a clampear
NOT OSERONT AND NOT T2 AND MCLAMPED = RES MCLAMPED ; descamplear
DFD MCLAMPED = RES OUNCLAMP   ; clampear
;
;
;
; cargar registros de depuracion
;() = CNCRD(FLWEY, RFLWEY, MTEMP)
;= CNCRD(TPOSY, RTPOSY, MTEMP)
;= CNCRD(APOSY, RAPOSY,MTEMP)
;= CNCRD(POSY,R210,MTEMP)
;= CNCRD(ATPOSY,R211,MTEMP)
;= CNCRD(DPLYY,R212,MTEMP)
;
; ------ control por comando T ---------------------------------
DFU TSTROBE = RES AUXEND
= MOV TBCD RTBCD 1032       ; copiar proxima herramienta origen bcd destino bin
= SBS RTBCD 1 RTBCD         ; restar 1
= MLS RTBCD DISTTOOL RTBCDD ; pasar a grados
= CNCRD(ATPOSY, RPOST, MTEMP); leer posición actual de torreta
= DVS RPOST 10000 RPOST     ; pasar a grados
= MDS RPOST 360 RPOST       ; pasar a 0<=X<=360
= SET MTSTROBE
= SBS RTBCDD RPOST RDELTAT  ; calcular delta de posición
CPS RDELTAT LE 3 AND CPS RDELTAT GE -3 = MDELTAT
;
; si la posición de la torreta es menor a la necesaria para cambiar evitar
; desclampeo
IMA DFU MTSTROBE AND MDELTAT = SET MTOOLF ; dar tiempo a TG6
= RES AUXEND ; reiniciar auxend
MTOOLF = TG1 6 40 ; retener auxend por 40ms
T6 = MAUXEND ; conectar timer a marca
DFD MAUXEND = SET AUXEND
= RES MTOOLF; liberar MTOOLF
= RES MTSTROBE; liberarl cambio de herramienta
;
;
; si hay un cambio real de herramienta entonces realizar proceso de cambio
IMA DFU MTSTROBE AND NOT MDELTAT = SET MTCHANGE ; descamplear
= SET MTCHANA2 ; proceso en ejecucion
;
DFU OSERONT AND MTCHANGE = RES MTCHANA1 ; esperar que servo este listo
= CNCWR(RTBCDD,GUP 200, MTEMP)
= CNCEX(G90 G0 Y P200, MTEMP)
= CNCEX((PLCMM 1032=1), MTEMP); MTCHANA1 alto cuando termina ejec
;
IMA DFU MTCHANA1 AND MTCHANGE = RES MTCHANGE; clampear
;
DFD ICLAMP AND MTCHANA2 = SET AUXEND; esperar clampeo
= RES MTCHANA2; proceso terminado
= RES MTCHANA1
= RES MTCHANA0
= RES MTSTROBE ; liberar proceso de cambio de herramienta
;= CNCEX(G92 YP200, MTEMP)          ; setear eje
;
;
; TODO: comprobar que herramienta mostrada coinicide con posicion de torreta
;
; ----- reiniciar torreta - M200 -------------------------------
DFU M200 = RES AUXEND
= SET MTHOMEA1; proceso en ejecucion
= CNCRD(TOOL, RTOOL, MTEMP) ; leer herramienta de CNC
= MOV RTOOL RTOOL 1032      ; convertir bcd a bin
= SBS RTOOL 1 RTOOL         ; TOOL comienza a contar en 1
= MLS RTOOL DISTTOOL RTOOLD ; pasar a gados
= SET MTHOME  ; descamplear y esperar senal de T1
;
DFU OSERONT AND MTHOME = RES MTHOMEA2; esperar servo listo
= CNCEX(G74 Y, MTEMP)          ; buscar inicio
= CNCEX(G91 G1 YP2000 F100., MTEMP)  ; mover a offset en coordenadas relativas
= CNCEX(G92 Y0, MTEMP)          ; setear eje a 0
= CNCEX((PLCMM 1022=1), MTEMP) ; avisar plc termino cnc MTHOMEA2
;
DFU MTHOMEA2 = RES MTHOMEA3 ; home encontrado,
; llevar a herramienta seleccionada en CNC
= CNCWR(RTOOLD,GUP 203, MTEMP)
= CNCEX(G1 YP203 F600. , MTEMP)
= CNCEX((PLCMM 1023=1), MTEMP) ; avisar plc termino cnc MTHOMEA3
DFU MTHOMEA3 AND MTHOME = RES MTHOME; iniciar proceso de clampeo
;
DFD ICLAMP AND MTHOMEA1 = SET AUXEND; esperar clampeo
= RES MTHOMEA1; proceso terminado
= RES MTHOMEA2
= RES MTHOMEA3
= CNCEX(G92 YP203, MTEMP)          ; setear eje
;
;
; ------------------------- control modo de fijación ----------
DFU M50 = SET MDIAMINT
DFU M51 = RES MDIAMINT
;
; ---------- retención de auxend para comandos M lógicos ------
;
; ------------------------- control por pedales ---------------
;
;
; ---- control de cabezal ------
; la pieza se encuentra clampeada dependiendo de si se encuentra
; trabajando en modo diametro interior o diametro exterior
NOT (OSPCLAMP XOR MDIAMINT) = MPARTCLA
;
; leer velocidad
() = CNCRD(SREAL, RSREAL, MTEMP )
; guardar si cabezal gira a menos de 10rpm
CPS RSREAL LE 100000 AND NOT M3 AND NOT M4 = MSPINDEA
; por cuestiones de seguridad solo se tiene que poder atrapar o
; liberar la pieza cuando no hay velocidad de giro real
DFU IPEDALCLA AND MSPINDEA AND OSPCLAMP = RES OSPCLAMP
DFU IPEDALCLA AND MSPINDEA AND NOT OSPCLAMP = SET OSPCLAMP
;
; ---- control de contra punta -----
; MTAILRDO AND MTAILFDO = MTAILPRO
; NOT MTAILPRO AND DFU IPEDALTAI AND MTAILPED
; = SET OTAILR
; = SET OTAILHS
; = RES MTAILPED
;
; NOT MTAILPRO AND DFU IPEDALTAI AND NOT MTAILPED
; = SET OTAILF
; = SET MTAILPED
;
; NOT MTAILPRO AND DFD IPEDALTAI
; = RES OTAILR
; = RES OTAILF
; = RES OTAILHS
;
;
; ------------------ control part catcher ----------------------
DFU M53 AND NOT OPARTCAT = SET OPARTCAT
DFU M63 AND OPARTCAT = RES OPARTCAT
DFU M53 OR DFU M63 = RES AUXEND
DFD M53 OR DFD M63 = TG1 5 TIMEPACA
T5 = MPARTCAT
DFD MPARTCAT = SET AUXEND
;
;
; ------------------------- fin programa pricipal --------------
END
