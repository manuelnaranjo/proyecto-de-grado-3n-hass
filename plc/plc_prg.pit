%TALLER VER 0.0.1    ,MX--,
;%Taller ver 0.0.1,MX--,
;
; *******************  DEFINICION DE NEMONICOS  ******************
;
;   ENTRADAS
;   ========
DEF IEMER    I01
; Entrada del boton de emergencia
DEF IREF0X   I02
; Origen de maquina eje X
DEF IREF0Z   I04
; Origen de maquina eje Z
DEF IREF0T   I06
; Origen de maquina torreta
DEF ICLAMP   I08
; Torreta campleada
DEF IUNCLAMP I10
; Torreta descampleada
DEF IEJEXOK  I03
; Entrada Alarma eje X
DEF IEJEZOK  I05
; Entrada Alarma eje Z
DEF IEJETOK  I07
; Entrada Alarma torreta
DEF IMACHON  I16
; Entrada Machine ON
DEF IREGTEMP I14
; Alarma de temperatura de la resistencia de regeneracion
DEF ISENAIR  I12
; Sensor de presion de aire
DEF ILLUB190 I67
; Sensor de bajo lubricante 190
DEF ILLUB960 I69
; Sensor de bajo lubricante 960
DEF IDOOR    I65
; Sensor de puerta cerrada
DEF ISPOVLOA I71
; SPINDLE OVERLOAD
DEF ISPPOWAL I73
; SPINDLE POWER FAILURE
DEF ISPSU    I75
;SPINDLE UP TO FREQUENCY
DEF ISPALAR  I77
; SPINDLE ALARM
;
;
;   SALIDAS
;   ========
;
DEF OEMER    O01
; PLC emergencia
; DEF OSYSENA  O02
; SYSTEM ENABLE
DEF OSERONX  O03
; Activa driver servo x
DEF OSERONZ  O05
; Activa driver servo z
DEF OSERONT  O07
; Activa driver servo torreta
DEF OALRESX  O02
; Resetea alarma servo x
DEF OALRESZ  O04
; Resetea alarm servo z
DEF OALREST  O06
; Reseta alarma torreta
DEF OUNCLAMP O48
; Cuando activado descamplea torreta
DEF OPARTCAT O46
; Part catcher
DEF OLUBAIR  O44
; Lubricacion con Aire
DEF OSPSTOP  O42
; Detener spindle
DEF OALRESSP O40
; Spindle reset alarma
DEF OSPFW    O38
; Spindle habilitar direccion positiva
DEF OSPRW    O36
; Spindle habilitar direccion reversa
;
;
;   CONSTANTES
;   ============
DEF SPVMIN 500000
; spindle velocidad minima
;
;
;   MARCAS
;   ============
DEF MTEMP    M1000
; Marca usada para CNCRD, CNCWR y CNCEX
DEF MANCLAMP M1020
; Marca clampeo torreta manual
DEF MANCLAAU M1021
; Marca auxiliar clampeo torreta manual
DEF MANTAU   M1022
; Marca auxiliar de que se mando avance manual a la torreta
;   REGISTROS
;   ============
DEF RSREAL   R10
; Registro utilizado para guardar SREAL del CNC
;
;   TIMERS
;   ============
;
;   CONTADORES
;   ============
;
; ******************** PROGAMA INICIAL **************************
CY1                     ; CICLO INICIAL
()                      ; NIVEL LOGICO ALTO, ejecuta siempre
=     ERA M1 149        ; BORRA LAS MARCAS
=     ERA M152 159      ; BORRA LAS MARCAS
=     ERA M161 2000     ; BORRA LAS MARCAS
=     ERA R1 20         ; BORRA LOS REGISTROS
=     ERA R23 254       ; BORRA LOS REGISTROS
=     ERA T1 255        ; BORRA LOS TEMPORIZADORES
=     ERA O1 255        ; BORRA LAS SALIDAS
=     SET GEAR1         ; SETEA GAMA UNO
=     SET OALRESX       ; reset alarma eje x
=     SET OALRESZ       ; reset alarma eje z
=     SET OALREST       ; reset alarma torreta
=     SET OALRESSP      ; reset alarma spindle
=     SET AUXEND        ; avisar al CNC que puede continuar
=     RES OUNCLAMP      ; clampear
=     RES OPARTCAT      ; desactivar part catcher
=     RES OLUBAIR       ; desactivar lubricación
=     SET OSPSTOP       ; frenar spindle
END                     ; FIN DEL CICLO INICIAL
;
;
;
; ********************* PROGAMA PRINCIPAL **********************
;
PRG                     ; PROGAMA PRINCIPAL
REA                     ; VALORES REALES
;
; plc status
IEMER = /EMERGEN;
()    = /STOP           ; canal plc en stop
()    = /FEEDHOL;
START = CYSTART;
INCYCLE = TIMERON;
;
; habilitar ejes si es posible
( IEJEXOK AND IMACHON ) = SERVOXON = OSERONX
( IEJEZOK AND IMACHON ) = SERVOZON = OSERONZ
;
; Conectar sensores de inicio máquina
IREF0X=DECELX;
IREF0Z=DECELZ;
IREF0T=DECELY;
;
;
; -----------Cabezal---------
() = GEAR1                  ; unico cambio
() = CNCRD(SREAL, RSREAL, MTEMP)
; TODO: agregar codigo para M19 luego de comprobar si el inversor
;       se lo banca
; TODO: agegar codigo delay de inicializacion si fuese necesario
;
;
; -----------Marcas enviadas por el CNC --------------------
;DFU MSTROBE AND CPS MBCD* EQ $0 =  M0    ; parada programada
DFU MSTROBE AND CPS MBCD* EQ $1 =  M1    ; parada opcional
DFU MSTROBE AND CPS MBCD* EQ $2 =  M2    ; simil m30
DFU MSTROBE AND CPS MBCD* EQ $3 =  M3    ; spindle a favor reloj
DFU MSTROBE AND CPS MBCD* EQ $4 =  M4    ; spindle contra reloj
DFU MSTROBE AND CPS MBCD* EQ $5 =  M5    ; parar spindle
DFU MSTROBE AND CPS MBCD* EQ $8 =  M8    ; activar refrigerante
DFU MSTROBE AND CPS MBCD* EQ $9 =  M9    ; desactivar refrigerante
DFU MSTROBE AND CPS MBCD* EQ $10 = M10
DFU MSTROBE AND CPS MBCD* EQ $11 = M11
DFU MSTROBE AND CPS MBCD* EQ $12 = M12
DFU MSTROBE AND CPS MBCD* EQ $13 = M13
DFU MSTROBE AND CPS MBCD* EQ $19 = M19   ; spindle lazo cerrado
DFU MSTROBE AND CPS MBCD* EQ $20 = M20
DFU MSTROBE AND CPS MBCD* EQ $21 = M21
DFU MSTROBE AND CPS MBCD* EQ $24 = M24
DFU MSTROBE AND CPS MBCD* EQ $25 = M25
DFU MSTROBE AND CPS MBCD* EQ $29 = M29
DFU MSTROBE AND CPS MBCD* EQ $30 = M30   ; fin de programa
; comandos macro personalizados
DFU MSTROBE AND CPS MBCD* EQ $200 = M200 ; inicializar torreta
DFU MSTROBE AND CPS MBCD* EQ $201 = M201 ; descamplear
DFU MSTROBE AND CPS MBCD* EQ $202 = M202 ; camplear
DFU MSTROBE AND CPS MBCD* EQ $203 = M203 ; avance lento torreta +
DFU MSTROBE AND CPS MBCD* EQ $204 = M204 ; avance lento torreta -
;
;
; -------- Control manual de la torreta de herramientas ---------
IMA DFD M201 = SET MANCLAMP = SET MANCLAAU
IMA DFD M202 = RES MANCLAMP = SET MANCLAAU
;
; si usuario solicito clampeo manual esperar a que termine de
; cambiar el actuador previo a permitir al CNC continuar
IMA MANCLAAU AND ( DFD ICLAMP OR DFD IUNCLAMP ) = SET AUXEND
= RES MANCLAAU
DFU MANCLAAU = RES AUXEND
;
; control de avance manual, se setea marca de control, se resetea
; auxend y se setea una vez que se alcanzo la posicion
IMA DFD M203 AND MANCLAMP = SET MANTAU ; avance lento +
= RES AUXEND
= CNCEX(G91 G0 Y.125, MTEMP)
IMA DFD M204 AND MANCLAMP = SET MANTAU ; avance lento -
= RES AUXEND
= CNCEX(G91 G0 Y-.125, MTEMP)
IMA DFD M206 AND MANCLAMP = SET MANTAU ; avance rapido +
= RES AUXEND
= CNCEX(G91 G0 Y36, MTEMP)
IMA DFD M205 AND MANCLAMP = SET MANTAU ; avance rapido -
= RES AUXEND
= CNCEX(G91 G0 Y-36, MTEMP)
; cuando servo alcance posicion
MANTAU AND IMA DFU INPOSY = SET  AUXEND
= RES MANTAU
;
; -------- Control temporal por teclado de torreta manual -------
; TODO: esto tiene que desaparacer en el codigo final
DFU B6R563 = SET M201
DFD B6R563 = RES M201
DFU B5R563 = SET M202
DFD B5R563 = RES M202
DFU B4R563 = SET M203
DFD B4R563 = RES M203
DFU B3R563 = SET M204
DFD B3R563 = RES M204
DFU B2R563 = SET M205
DFD B2R563 = RES M205
DFU B1R563 = SET M206
DFD B1R563 = RES M206
;
; CONTROL DIRECCION SPINDLE
( ) = CNCRD(SPEED, R11, M1001 )
( DM03 OR DM19 ) AND ( CPS R11 GE 500000 )  = O38
;( DFU M05 OR ( ( DFU DM03 OR DFU DM04 ) AND ( CPS R10 LT 50 ) ) )  = O36 = O38
( DM05 ) OR ( CPS R11 LT 500000 ) = O40
( DM04 ) AND ( CPS R11 GE 500000 ) = O36
;
;
; CONTROL TORRETA
( I7 AND I16 ) = SERVOYON; CONTROL SERVO
DFU TSTROBE OR DFU M1100 = CNCRD(TOOL, R20, M2000) = SET M1999
IMA DFU M1999 AND CPS R20 LE $10 = JMP L1
IMA DFU M1999 = SBS R20 6 R20
L1
IMA DFU M1999 = SBS R20 1 R20
= MLS R20 -36 R12
= RES M1999
( NOT I10 ) = O7
;( NOT I10 AND NOT M1020 ) = O7
M1002 OR M1100 OR MANCLAMP = O48; CONTROL ACTUADOR
( ) = CNCRD(POSY, R200, M2000)
= CNCRD(FLWEY, R201, M2000)
= CNCRD(DISTY, R202, M2000)
= CNCRD(TPOSY, R203, M2000)
DFU M202 = RES M201
;
; CAMBIO T
DFU TSTROBE = RES AUXEND = SET M1002
= MOV TBCD R13
DFU TSTROBE AND CPS R13 GE $10 = SBS R13 6 R13
IMA DFD I10 AND M1002 = SET M1004
= SBS R13 1 R13
= RES M1007
= MLS -36 R13 R23
= CNCWR(R23 ,GUP200,M1006)
= CNCEX(G90 G0 Y P200, M1006)
= CNCEX((PLCM 1007=1), M1006)
IMA DFU M1007 AND M1004 = RES M1002 = RES M1004
DFD I8 = SET AUXEND
;
; RESET TOOL CHANGER
DFU M200 = RES AUXEND = SET M1100
DFD I10 AND M1100 = RES M1117
= CNCEX(G74 Y, M1107)
;= CNCEX(G4, M1107)
;= CNCEX(G90 G0 YP2000, M1107)
;= CNCEX(G92Y0, M1107)
;= CNCEX(G4, M1107)
= CNCEX((PLCM 1117=1), M1107)
DFU M1117 = RES M1118
= CNCWR(R12,GUP200,M2000)
;= CNCEX(G90 G0 Y P200, M1107)
;= CNCEX(G4, M1107)
= CNCEX((PLCM 1118=1),M1107)
DFU M1118 = RES M1100
DFD  I8 = SET AUXEND
END
