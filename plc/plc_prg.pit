%TALLER VER 0.1.0    ,MX--,
;%Taller ver 0.1.0,MX--,
;
; *******************  DEFINICION DE NEMONICOS  ******************
;
;   ENTRADAS
;   ========
DEF IEMER    I01
; Entrada del boton de emergencia
DEF IREF0X   I02
; Origen de maquina eje X
DEF IREF0Z   I04
; Origen de maquina eje Z
DEF IREF0T   I06
; Origen de maquina torreta
DEF ICLAMP   I08
; Torreta campleada
DEF IUNCLAMP I10
; Torreta descampleada
DEF IEJEXOK  I03
; Entrada Alarma eje X
DEF IEJEZOK  I05
; Entrada Alarma eje Z
DEF IEJETOK  I07
; Entrada Alarma torreta
DEF IMACHON  I16
; Entrada Machine ON
DEF IREGTEMP I09
; Alarma de temperatura de la resistencia de regeneracion
DEF ISENAIR  I12
; Sensor de presion de aire
DEF ILLUB190 I67
; Sensor de bajo lubricante 190
DEF ILLUB960 I69
; Sensor de bajo lubricante 960
DEF IDOOR    I65
; Sensor de puerta cerrada
DEF ISPOVLOA I71
; Spindle Sobrecarga
DEF ISPPOWAL I73
; Spindle Falla Energia
DEF ISPSU    I75
; Spindle Frequencia Alcanzada
DEF ISPALAR  I77
; Spindle Alarma
DEF ILOWHIDR I14
; Baja presión en sistema hidraulico NA
DEF ITAILFIN1 I11
; Contra punta cerca pieza
DEF ITAILFIN2 I13
; Contra punta punto medio
DEF ITAILFIN3 I15
; Contra punta alejada pieza
DEF IPEDALCLA I77
; Pedal de clampeo/desclampeo de pieza
DEF IPEDALTAI I84
;
;
;
;   SALIDAS
;   ========
;
DEF OEMER    O01
; PLC emergencia
; DEF OSYSENA  O02
; SYSTEM ENABLE
DEF OSERONX  O03
; Activa driver servo x
DEF OSERONZ  O05
; Activa driver servo z
DEF OSERONT  O07
; Activa driver servo torreta
DEF OALRESX  O02
; Resetea alarma servo x
DEF OALRESZ  O04
; Resetea alarm servo z
DEF OALREST  O06
; Reseta alarma torreta
DEF OUNCLAMP O48
; Cuando activado descamplea torreta
DEF OPARTCAT O46
; Part catcher
DEF OLUBAIR  O44
; Lubricacion con Aire
DEF OSPSTOP  O42
; Detener spindle
DEF OALRESSP O40
; Spindle reset alarma
DEF OSPFW    O38
; Spindle habilitar direccion positiva
DEF OSPRW    O36
; Spindle habilitar direccion reversa
;
DEF OTAILR   O33
; Alejar contra punta de la pieza
DEF OTAILF   O35
; Acercar contra punta a la pieza
DEF OTAILHS  O39
; Mover contrapunta a toda velocidad
DEF OSPCLAMP O37
; Liberar pieza
;
DEF OLUZVERD O45
; Prender durante ciclo de trabajo
DEF OLUZROJA O47
; Prender en caso de alarma
;
;   CONSTANTES
;   ============
DEF SPVMIN 500000
; spindle velocidad minima, por abajo de 50rpm no regula el driver
DEF DISTTOOL -36
; una herramienta cada 36 grados, sentido negativo
;
DEF TIMECLA  100
; 75 ms timeout al clampear
DEF TIMEUCLA 50
; 50 ms timeout al desclampear
;
;
DEF TIMETAIL 5500
; tiempo a velocidad lenta entre que pasa fin1 y llega al usillo
DEF TAILZON0 1
; contra punta entre fin 1 y pieza (zona 0)
DEF TAILZON1 2
; contra punta entre fin 1 y fin 2 (zona 1)
DEF TAILZON2 3
; contra punta entre fin 2 y fin 3 (zona 2)
DEF TAILFIN3 4
; contra punta en fin 3
;
;   MARCAS
;   ============
DEF MTEMP    M1000
; Marca usada para CNCRD, CNCWR y CNCEX
DEF MANCLAMP M1010
; Marca clampeo torreta manual
DEF MANCLAAU M1011
; Marca auxiliar clampeo torreta manual
DEF MANTAU   M1012
; Marca auxiliar de que se mando avance manual a la torreta
DEF MTHOME   M1020
; Inicializar torreta de herramientas
DEF MTHOMEA1 M1021
; auxiliar inicializar torreta herramientas 1
DEF MTHOMEA2 M1022
; auxiliar inicializar torreta herramientas 2
DEF MTHOMEA3 M1023
; auxiliar inicializar torreta herramientas 3
DEF MTHOMEA0 M1024
; auxiliar inicializar torreta herramientas 0
DEF MTCHANGE M1030
; cambio de herramienta en progreso por comando T
DEF MTCHANA0 M1031
; cambio de herramienta en progreso por comando T auxiliar 0
DEF MTCHANA1 M1032
; cambio de herramienta en progreso por comando T auxiliar 1
DEF MTCHANA2 M1033
; cambio de herramienta en progreso por comando T auxiliar 2
;
DEF MCLAMPED M1040
; torreta clampeada
DEF MUCLAMPE M1041
; torreta descampleada
;
DEF MACKSPIN M1050
; usado para hacer el ACK de los comandos del spindle M03, M04, M05, M19
;
DEF MTIMTAIL M1060
; usado para controlar timer para control de la contra punta
DEF MTAILFI3 M1061
; usado para marcar al llegar contra punta a fin3
DEF MTAILZO2 M1062
; usado para marcar al llegar contra punta a zona2
DEF MTAILZO1 M1063
; usado para marcar al llegar contra punta a zona1
DEF MTAILZO0 M1064
; usado para marcar al llegar contra punta a zona0
DEF MTAILRDO M1065
; usado para marcar fin de proceso de retroceso
DEF MTAILFDO M1066
; usado para detectar flancos en T4
DEF MT4      M1067
; usado para marcar contra punta programada
DEF MTAILPRO M1068
; usado para marcar direccion contra punta por pedal
DEF MTAILPED M1069
;
; control de clampeo de spindle
; modo diametro interior / diametro exterior 1 en modo diametro interior
DEF MDIAMINT M1080
; pieza atrapada
DEF MPARTCLA M1081
; spindle quieto
DEF MSPINDEA M1082
;
;
;
;   REGISTROS
;   ============
DEF RSREAL   R10
; Registro utilizado para guardar SREAL del CNC
DEF RSPEED   R11
; Registro utilizado para guardar velocidad programada del spindle
DEF RTOOL    R12
; Registro utilizado para guardar la herramienta seleccionada -1
DEF RTOOLD   R13
; herramienta seleccionada en grados
DEF RTBCD    R14
; proxima herramienta pasada por comando T
DEF RTBCDD   R15
; proxima herramienta pasada por comando T en gradosx
DEF RPOSTAIL R300
; posicion contra punta
;          posibles valores:
;                   0 - No inicializado
;                   1 - Pieza
;                   2 - Fin 1
;                   3 - Fin 2
;                   4 - Fin 3
DEF RPOSY    R200
; debug posicion torreta
DEF RFLWEY   R201
; debug error posicion torreta
DEF RTPOSY   R202
; debug posicion teorica torreta
DEF RAPOSY   R203
;
;   TIMERS
;   ============
;   T1 usado para esperar previo a desclampeo torreta
;   T2 usado para esperar previo a clampeo torreta
;   T3 usado control dirección de spindle
;   T4 usado para contar el tiempo que transcurre desde que la contra punta
;   entra a zona 0 hasta que la misma haya llegado a la pieza, como no hay
;   sensores disponibles no puedo saber si llego a tocar
;
;   CONTADORES
;   ============
;
; ******************** PROGAMA INICIAL **************************
CY1                     ; CICLO INICIAL
()                      ; NIVEL LOGICO ALTO, ejecuta siempre
=     ERA M1 149        ; BORRA LAS MARCAS
=     ERA M152 159      ; BORRA LAS MARCAS
=     ERA M161 1059     ; BORRA LAS MARCAS
=     ERA M1080 2000    ; BORRA LAS MARCAS
=     ERA R1 20         ; BORRA LOS REGISTROS
=     ERA R23 254       ; BORRA LOS REGISTROS
=     ERA T1 255        ; BORRA LOS TEMPORIZADORES
=     ERA O1 255        ; BORRA LAS SALIDAS
=     SET GEAR1         ; SETEA GAMA UNO
=     SET OALRESX       ; reset alarma eje x
=     SET OALRESZ       ; reset alarma eje z
=     SET OALREST       ; reset alarma torreta
=     RES OALRESSP      ; reset alarma spindle
=     SET AUXEND        ; avisar al CNC que puede continuar
=     RES OUNCLAMP      ; clampear
=     RES OPARTCAT      ; desactivar part catcher
=     RES OLUBAIR       ; desactivar lubricación
=     SET OSPSTOP       ; frenar spindle
=     CNCRD(GUP 1000, R1, MTEMP) ; leer parametro general 1000
CPS R1 EQ 1 = MDIAMINT  ; cargar modo diametro interior, diametro exterior
END                     ; FIN DEL CICLO INICIAL
;
;
;
; ********************* PROGAMA PRINCIPAL **********************
;
PRG                     ; PROGAMA PRINCIPAL
REA                     ; VALORES REALES
;
; plc status
IEMER = /EMERGEN;
; canal plc en stop
()    = /STOP
; habilitar avance de ejes
()    = /FEEDHOL;
; no permitir iniciar programa a menos que la pieza se encuentra agarrada
START AND MPARTCLA = CYSTART;
INCYCLE = TIMERON;
;
; habilitar ejes si es posible
( IEJEXOK AND IMACHON ) = SERVOXON = OSERONX
( IEJEZOK AND IMACHON ) = SERVOZON = OSERONZ
;
; Conectar sensores de inicio máquina
IREF0X=DECELX;
IREF0Z=DECELZ;
IREF0T=DECELY;
;
;
; -----------Cabezal---------
() = GEAR1                  ; unico cambio
= SPENAS
= DRENAS
= SERVOSON
; TODO: agregar codigo para M19 luego de comprobar si el inversor
;       se lo banca
; TODO: agegar codigo delay de inicializacion si fuese necesario
;
;
; -----------Marcas enviadas por el CNC --------------------
;DFU MSTROBE AND CPS MBCD* EQ $0 =  M0    ; parada programada
DFU MSTROBE AND CPS MBCD* EQ $1 =  M1    ; parada opcional
DFU MSTROBE AND CPS MBCD* EQ $2 =  M2    ; simil m30
DFU MSTROBE AND CPS MBCD* EQ $3 =  M3    ; spindle a favor reloj
DFU MSTROBE AND CPS MBCD* EQ $4 =  M4    ; spindle contra reloj
DFU MSTROBE AND CPS MBCD* EQ $5 =  M5    ; parar spindle
DFU MSTROBE AND CPS MBCD* EQ $8 =  M8    ; activar refrigerante
DFU MSTROBE AND CPS MBCD* EQ $9 =  M9    ; desactivar refrigerante
DFU MSTROBE AND CPS MBCD* EQ $10 = M10
DFU MSTROBE AND CPS MBCD* EQ $11 = M11
DFU MSTROBE AND CPS MBCD* EQ $12 = M12
DFU MSTROBE AND CPS MBCD* EQ $13 = M13
DFU MSTROBE AND CPS MBCD* EQ $19 = M19   ; spindle lazo cerrado
DFU MSTROBE AND CPS MBCD* EQ $20 = M20
DFU MSTROBE AND CPS MBCD* EQ $21 = M21
DFU MSTROBE AND CPS MBCD* EQ $24 = M24
DFU MSTROBE AND CPS MBCD* EQ $25 = M25
DFU MSTROBE AND CPS MBCD* EQ $29 = M29
DFU MSTROBE AND CPS MBCD* EQ $30 = M30   ; fin de programa
;
; ------------ comandos macro personalizados --------------------------
; comandos
DFU MSTROBE AND CPS MBCD* EQ $53  = M53  ; activar part catcher
DFU MSTROBE AND CPS MBCD* EQ $63  = M63  ; desactivar part catcher
DFU MSTROBE AND CPS MBCD* EQ $200 = M200 ; inicializar torreta
; --------------------
; TODO: estos comandos tienen que volar de la version final
DFU MSTROBE AND CPS MBCD* EQ $201 = M201 ; descamplear torreta
DFU MSTROBE AND CPS MBCD* EQ $202 = M202 ; camplear torreta
DFU MSTROBE AND CPS MBCD* EQ $203 = M203 ; avance lento contra punta +
DFU MSTROBE AND CPS MBCD* EQ $204 = M204 ; avance rapido contra punto -
; fin de TODO
;
;
; control contra punta
; retroceso rapido
DFU MSTROBE AND CPS MBCD* EQ $300 = SET M300 ; hasta tope 1
DFU MSTROBE AND CPS MBCD* EQ $301 = SET M301 ; hasta tope 2
DFU MSTROBE AND CPS MBCD* EQ $302 = SET M302 ; hasta tope 3
; avance rapudo hasta tope X luego lento por TIMETAIL ms
DFU MSTROBE AND CPS MBCD* EQ $310 = SET M310 ; rapido hasta tope 1
DFU MSTROBE AND CPS MBCD* EQ $311 = SET M311 ; rapido hasta tope 2
DFU MSTROBE AND CPS MBCD* EQ $312 = SET M312 ; lento
;
;
; -------- Control manual de la torreta de herramientas ---------
DFU M201 = RES M202
DFU M202 = RES M201
IMA DFD M201 = SET MANCLAMP = SET MANCLAAU
IMA DFD M202 = RES MANCLAMP = SET MANCLAAU
;
; si usuario solicito clampeo manual esperar a que termine de
; cambiar el actuador previo a permitir al CNC continuar
IMA MANCLAAU AND ( DFD ICLAMP OR DFD IUNCLAMP ) = SET AUXEND
= RES MANCLAAU
DFU MANCLAAU = RES AUXEND
;
; control de avance manual, se setea marca de control, se resetea
; auxend y se setea una vez que se alcanzo la posicion
IMA DFD M203 AND MANCLAMP = SET MANTAU ; avance lento +
= RES AUXEND
= CNCEX(G1 G91 Y.125 F700., MTEMP)
IMA DFD M204 AND MANCLAMP = SET MANTAU ; avance lento -
= RES AUXEND
= CNCEX(G1 G91 Y-.125 F700, MTEMP)
IMA DFD M206 AND MANCLAMP = SET MANTAU ; avance rapido +
= RES AUXEND
= CNCEX(G91 G0 Y36, MTEMP)
IMA DFD M205 AND MANCLAMP = SET MANTAU ; avance rapido -
= RES AUXEND
= CNCEX(G91 G0 Y-36, MTEMP)
; cuando servo alcance posicion
MANTAU AND IMA DFU INPOSY = SET  AUXEND
= RES MANTAU
;
; -------- Control temporal por teclado de torreta manual -------
; TODO: esto tiene que desaparacer en el codigo final
DFU B6R563 = SET M201
DFD B6R563 = RES M201
DFU B5R563 = SET M202
DFD B5R563 = RES M202
DFU B4R563 = SET M203
DFD B4R563 = RES M203
DFU B3R563 = SET M204
DFD B3R563 = RES M204
DFU B2R563 = SET M205
DFD B2R563 = RES M205
DFU B1R563 = SET M206
DFD B1R563 = RES M206
;
;
;--------- Control manual de contra punta -----------------------
; si llega a fin3 entonces esta en fin 3
DFU ITAILFIN3 = MOV TAILFIN3 RPOSTAIL
; si llega a fin2 y se aleja de la pieza o si se alejo de fin3
; se encuentra en en zona 2
(DFU ITAILFIN2 AND OTAILR) OR DFD ITAILFIN3 = MOV TAILZON2 RPOSTAIL;
; si llega a fin1 y se aleja de la pieza, o si se acerca a la pieza y toca fin 2
; se encuentra en zona 1
(DFU ITAILFIN1 AND OTAILR) OR (DFD ITAILFIN2 AND OTAILF) = MOV TAILZON1 RPOSTAIL
; si se aleja de fin1 y se acerca a la pieza entonces alcanzo zona 0
DFD ITAILFIN1 AND OTAILF = MOV TAILZON0 RPOSTAIL
; cualquier otro caso no se inicializo y se mueve lentamente hasta entrar
; en estado válido, esto sólo sucede la primera vez que se ejecute esté código
; ya que el registro mantiene el valor luego de perder la energía y no es borrado.
;
; setear marcas para detectar transiciones
CPS RPOSTAIL EQ TAILFIN3 = MTAILFI3
CPS RPOSTAIL EQ TAILZON2 = MTAILZO2
CPS RPOSTAIL EQ TAILZON1 = MTAILZO1
CPS RPOSTAIL EQ TAILZON0 = MTAILZO0
;
; desactivar contra punta cuando llega a fin3
DFU MTAILFI3 AND OTAILR = RES OTAILR = RES OTAILHS
;
;
; marcar inicio de retroceso programado
DFU M300 OR DFU M301 OR DFU M302
= SET MTAILRDO
= RES AUXEND
;
; marcar fin de retroceso programado
(M300 AND DFU ITAILFIN1) OR
(M301 AND DFU ITAILFIN2) OR
(M302 AND DFU ITAILFIN3)
= RES MTAILRDO
= RES M300 = RES M301 = RES M302
= SET AUXEND
;
; marcar inicio avance programado
DFU M310 OR DFU M311 OR DFU M312
= SET MTAILFDO
= RES AUXEND
; marcar fin de avance programado
T4 = MT4
DFD MT4
= RES MTAILFDO
= SET AUXEND
= RES M310 = RES M311 = RES M312
= RES MTIMTAIL
;
; control avance contra punta
DFU B0R563 OR DFU MTAILFDO = SET OTAILF
DFD B0R563 OR DFD MTAILFDO = RES OTAILF
;
; control retroceso contra punta
DFU B7R563 OR DFU MTAILRDO = SET OTAILR
DFD B7R563 OR DFD MTAILRDO = RES OTAILR
;
; control velocidad rapida contra punta
DFU MTAILRDO OR
DFU B15R563 OR
MTAILFDO AND (
         ( NOT MTAILZO0 AND M310 ) OR
         ( NOT MTAILZO0 AND NOT MTAILZO1 AND M311 ) OR
         ( NOT MTAILZO0 AND NOT MTAILZO1 AND NOT MTAILZO2 AND M312)
)
= SET OTAILHS
DFD MTAILRDO OR DFD B15R563 = RES OTAILHS
; desactivar alta velocidad al entrar a zona 0 si M310
; desactivar alta velocidad al entrar a zona 1 si M311
; desactivar alta velocidad al entrar a zona 2 si M312
OTAILF AND (
       (DFU MTAILZO0 AND M310) OR
       (DFU MTAILZO1 AND M311) OR
       (DFU MTAILZO2 AND M312)
) = RES OTAILHS
= SET MTIMTAIL ; inicializar contador
MTIMTAIL = TG4 4 TIMETAIL
;
;
; -------- Control Direccion Spindle ----------------------------
() = CNCRD(SPEED, RSPEED, MTEMP )
( DM03 OR DM19 ) AND ( CPS RSPEED GE SPVMIN ) = OSPFW
( DM05 ) OR ( CPS RSPEED LT SPVMIN ) = OSPSTOP
( DM04 ) AND ( CPS RSPEED GE SPVMIN ) = OSPRW
DFU M3 OR DFU M4 OR DFU M5 OR DFU M19 OR DFU SSTROBE = SET MACKSPIN
= RES AUXEND
MACKSPIN = TG1 3 20         ; por 20 ms auxend es mio
NOT T3 AND MACKSPIN = SET AUXEND
= RES MACKSPIN
;
; -------- Control automatico Torreta herramientas --------------
( IEJETOK AND IMACHON ) = SERVOYON; habilitar lazo eje salvo que hayan alarmas
;
; control con retardos para evitar que lazo de control se active durante
; proceso de cambio de estado del actuador
DFU MTCHANGE OR DFU MTHOME OR DFU MANCLAMP = SET OUNCLAMP; descamplear
DFD IUNCLAMP = SET MUCLAMPE       ; marcar cuando se detecta desclampe
MUCLAMPE = TG4 1 TIMEUCLA         ; esperar Xms luego de descamplear
NOT IUNCLAMP AND NOT T1 AND MUCLAMPE = RES MUCLAMPE ; limpiar marca
DFD MUCLAMPE = SET OSERONT        ; cuando la marca se limpia, energizar servo
;
;
DFD MTCHANGE OR DFD MTHOME OR DFD MANCLAMP = RES OSERONT ; desenergizar servo
DFD OSERONT = SET MCLAMPED    ; marcar cuando servo se desenergiza
MCLAMPED = TG4 2 TIMECLA      ; esperar Xms previo a clampear
NOT OSERONT AND NOT T2 AND MCLAMPED = RES MCLAMPED; descamplear
DFD MCLAMPED = RES OUNCLAMP   ; clampear
;
;
;
; cargar registros de depuracion
() = CNCRD(POSY, RPOSY, MTEMP)
= CNCRD(FLWEY, RFLWEY, MTEMP)
= CNCRD(TPOSY, RTPOSY, MTEMP)
= CNCRD(APOSY, RAPOSY,MTEMP)
;
; ------ control por comando T ---------------------------------
DFU TSTROBE = RES AUXEND
= SET MTCHANGE ; descamplear
= SET MTCHANA2 ; proceso en ejecucion
= MOV TBCD RTBCD 1032     ; copiar origen bcd destino bin
= SBS RTBCD 1 RTBCD       ; restar 1
= MLS RTBCD DISTTOOL RTBCDD; pasar a grados
;
DFU OSERONT AND MTCHANGE = RES MTCHANA1 ; esperar que servo este listo
= CNCWR(RTBCDD,GUP 200, MTEMP)
= CNCEX(G90 G0 Y P200, MTEMP)
= CNCEX((PLCMM 1032=1), MTEMP); MTCHANA1 alto cuando termina ejec
;
IMA DFU MTCHANA1 AND MTCHANGE = RES MTCHANGE; clampear
;
DFD ICLAMP AND MTCHANA2 = SET AUXEND; esperar clampeo
= RES MTCHANA2; proceso terminado
= RES MTCHANA1
= RES MTCHANA0
;= CNCEX(G92 YP200, MTEMP)          ; setear eje
;
;
; ----- reiniciar torreta - M200 -------------------------------
DFU M200 = RES AUXEND
= SET MTHOMEA1; proceso en ejecucion
= CNCRD(TOOL, RTOOL, MTEMP) ; leer herramienta de CNC
= MOV RTOOL RTOOL 1032      ; convertir bcd a bin
= SBS RTOOL 1 RTOOL         ; TOOL comienza a contar en 1
= MLS RTOOL DISTTOOL RTOOLD ; pasar a gados
= SET MTHOME  ; descamplear y esperar senal de T1
;
DFU OSERONT AND MTHOME = RES MTHOMEA2; esperar servo listo
= CNCEX(G74 Y, MTEMP)          ; buscar inicio
= CNCEX(G91 G1 YP2000 F100., MTEMP)  ; mover a offset en coordenadas relativas
= CNCEX(G92 Y0, MTEMP)          ; setear eje a 0
= CNCEX((PLCMM 1022=1), MTEMP) ; avisar plc termino cnc MTHOMEA2
;
DFU MTHOMEA2 = RES MTHOMEA3 ; home encontrado,
; llevar a herramienta seleccionada en CNC
= CNCWR(RTOOLD,GUP 200, MTEMP)
= CNCEX(G1 YP200 F600. , MTEMP)
= CNCEX((PLCMM 1023=1), MTEMP) ; avisar plc termino cnc MTHOMEA3
DFU MTHOMEA3 AND MTHOME = RES MTHOME; iniciar proceso de clampeo
;
DFD ICLAMP AND MTHOMEA1 = SET AUXEND; esperar clampeo
= RES MTHOMEA1; proceso terminado
= RES MTHOMEA2
= RES MTHOMEA3
= CNCEX(G92 YP200, MTEMP)          ; setear eje
;
;
; ------------------------- control por pedales ---------------
;
; ---- control de cabezal ------
;
; la pieza se encuentra clampeada dependiendo de si se encuentra
; trabajando en modo diametro interior o diametro exterior
NOT (OSPCLAMP XOR MDIAMINT) = MPARTCLA
;
; leer velocidad
() = CNCRD(SREAL, RSREAL, MTEMP )
; guardar si cabezal esta quieto
CPS RSREAL EQ 0 = MSPINDEA
; por cuestiones de seguridad solo se tiene que poder atrapar o
; liberar la pieza cuando no hay velocidad de giro
DFU IPEDALCLA AND MSPINDEA AND OSPCLAMP = RES OSPCLAMP
DFU IPEDALCLA AND MSPINDEA AND NOT OSPCLAMP = SET OSPCLAMP
;
; ---- control de contra punta -----
MTAILRDO AND MTAILFDO = MTAILPRO
NOT MTAILPRO AND DFU IPEDALTAI AND MTAILPED
= SET OTAILR
= SET OTAILHS
= RES MTAILPED
;
NOT MTAILPRO AND DFU IPEDALTAI AND NOT MTAILPED
= SET OTAILF
= SET MTAILPED
;
NOT MTAILPRO AND DFD IPEDALTAI
= RES OTAILR
= RES OTAILF
= RES OTAILHS
;
;
; ------------------ control part catcher ----------------------
DFU M53 AND NOT OPARTCAT = SET OPARTCAT
DFU M63 AND OPARTCAT = RES OPARTCAT
DFU M53 OR DFU M63 = RES AUXEND
DFD M53 OR DFD M63 = SET AUXEND
;
; ------------------------- fin programa pricipal --------------
END
